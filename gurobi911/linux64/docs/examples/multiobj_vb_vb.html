<!DOCTYPE HTML>

<!--Converted with LaTeX2HTML 2019 (Released January 1, 2019) -->
<HTML lang="EN">
<HEAD>
<TITLE>multiobj_vb.vb</TITLE>
<META NAME="description" CONTENT="multiobj_vb.vb">
<META NAME="keywords" CONTENT="examples">
<META NAME="resource-type" CONTENT="document">
<META NAME="distribution" CONTENT="global">

<META HTTP-EQUIV="Content-Type" CONTENT="text/html; charset=utf-8">
<META NAME="viewport" CONTENT="width=device-width, initial-scale=1.0">
<META NAME="Generator" CONTENT="LaTeX2HTML v2019">

<LINK REL="STYLESHEET" HREF="examples.css">

<LINK REL="next" HREF="multiscenario_vb_vb.html">
<LINK REL="previous" HREF="mip2_vb_vb.html">
<LINK REL="next" HREF="multiscenario_vb_vb.html">
</HEAD>

<BODY >

<DIV CLASS="navigation"><!--Navigation Panel-->
<A
 HREF="multiscenario_vb_vb.html">
<IMG WIDTH="37" HEIGHT="24" ALT="next" SRC="next.png"></A> 
<A
 HREF="visual_basic_examples.html">
<IMG WIDTH="26" HEIGHT="24" ALT="up" SRC="up.png"></A> 
<A
 HREF="mip2_vb_vb.html">
<IMG WIDTH="63" HEIGHT="24" ALT="previous" SRC="prev.png"></A>   
<BR>
<B> Next:</B> <A
 HREF="multiscenario_vb_vb.html">multiscenario_vb.vb</A>
<B> Up:</B> <A
 HREF="visual_basic_examples.html">Visual Basic Examples</A>
<B> Previous:</B> <A
 HREF="mip2_vb_vb.html">mip2_vb.vb</A>
<BR>
<BR></DIV>
<!--End of Navigation Panel-->

<H3><A ID="SECTION000251700000000000000"></A>
<A ID="subsubsection:multiobj_vb.vb"></A>
<BR>
multiobj_vb.vb
</H3>
<SCRIPT SRC="https://cdn.rawgit.com/google/code-prettify/master/loader/run_prettify.js"></SCRIPT>
<BR>
<PRE CLASS="prettyprint">
' Copyright 2020, Gurobi Optimization, LLC

' Want to cover three different sets but subject to a common budget of
' elements allowed to be used. However, the sets have different priorities to
' be covered; and we tackle this by using multi-objective optimization.

Imports Gurobi

Class multiobj_vb

    Shared Sub Main()

        Try
            ' Sample data
            Dim groundSetSize As Integer = 20
            Dim nSubsets As Integer = 4
            Dim Budget As Integer = 12

            Dim [Set] As Double(,) = New Double(,) { _
                {1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0}, _
                {0, 0, 0, 0, 0, 1, 1, 1, 1, 1, 0, 0, 0, 0, 0, 1, 1, 1, 1, 1}, _
                {0, 0, 0, 1, 1, 0, 1, 1, 0, 0, 0, 0, 0, 1, 1, 0, 1, 1, 0, 0}, _
                {0, 0, 0, 1, 1, 1, 0, 0, 0, 1, 1, 1, 0, 0, 0, 1, 1, 1, 0, 0}}

            Dim SetObjPriority As Integer() = New Integer() {3, 2, 2, 1}
            Dim SetObjWeight As Double() = New Double() {1.0, 0.25, 1.25, 1.0}
            Dim e As Integer, i As Integer, status As Integer, nSolutions As Integer

            ' Create environment
            Dim env As New GRBEnv("multiobj_vb.log")

            ' Create initial model
            Dim model As New GRBModel(env)
            model.ModelName = "multiobj_vb"

            ' Initialize decision variables for ground set:
            ' x[e] == 1 if element e is chosen for the covering.
            Dim Elem As GRBVar() = model.AddVars(groundSetSize, GRB.BINARY)
            For e = 0 To groundSetSize - 1
                Dim vname As String = "El" &amp; e.ToString()
                Elem(e).VarName = vname
            Next

            ' Constraint: limit total number of elements to be picked to be at most
            ' Budget
            Dim lhs As New GRBLinExpr()
            For e = 0 To groundSetSize - 1
                lhs.AddTerm(1.0, Elem(e))
            Next
            model.AddConstr(lhs, GRB.LESS_EQUAL, Budget, "Budget")

            ' Set global sense for ALL objectives
            model.ModelSense = GRB.MAXIMIZE

            ' Limit how many solutions to collect
            model.Parameters.PoolSolutions = 100

            ' Set and configure i-th objective
            For i = 0 To nSubsets - 1
                Dim vname As String = String.Format("Set{0}", i)
                Dim objn As New GRBLinExpr()
                For e = 0 To groundSetSize - 1
                    objn.AddTerm([Set](i, e), Elem(e))
                Next

                model.SetObjectiveN(objn, i, SetObjPriority(i), SetObjWeight(i), _
                                    1.0 + i, 0.01, vname)
            Next

            ' Save problem
            model.Write("multiobj_vb.lp")

            ' Optimize
            model.Optimize()

            ' Status checking
            status = model.Status

            If status = GRB.Status.INF_OR_UNBD OrElse _
               status = GRB.Status.INFEASIBLE OrElse _
               status = GRB.Status.UNBOUNDED Then
                Console.WriteLine("The model cannot be solved " &amp; _
                         "because it is infeasible or unbounded")
                Return
            End If
            If status &lt;&gt; GRB.Status.OPTIMAL Then
                Console.WriteLine("Optimization was stopped with status {0}", status)
                Return
            End If

            ' Print best selected set
            Console.WriteLine("Selected elements in best solution:")
            Console.Write(vbTab)
            For e = 0 To groundSetSize - 1
                If Elem(e).X &lt; 0.9 Then
                    Continue For
                End If
                Console.Write("El{0} ", e)
            Next
            Console.WriteLine()

            ' Print number of solutions stored
            nSolutions = model.SolCount
            Console.WriteLine("Number of solutions found: {0}", nSolutions)

            ' Print objective values of solutions
            If nSolutions &gt; 10 Then
                nSolutions = 10
            End If
            Console.WriteLine("Objective values for first {0} solutions:", nSolutions)
            For i = 0 To nSubsets - 1
                model.Parameters.ObjNumber = i

                Console.Write(vbTab &amp; "Set" &amp; i)
                For e = 0 To nSolutions - 1
                    model.Parameters.SolutionNumber = e
                    Console.Write("{0,8}", model.ObjNVal)
                Next
                Console.WriteLine()
            Next

            model.Dispose()
            env.Dispose()

        Catch e As GRBException
            Console.WriteLine("Error code = {0}", e)
            Console.WriteLine(e.Message)

        End Try
    End Sub
End Class
</PRE>

<P>

<DIV CLASS="navigation"><HR>
<!--Navigation Panel-->
<A
 HREF="multiscenario_vb_vb.html">
<IMG WIDTH="37" HEIGHT="24" ALT="next" SRC="next.png"></A> 
<A
 HREF="visual_basic_examples.html">
<IMG WIDTH="26" HEIGHT="24" ALT="up" SRC="up.png"></A> 
<A
 HREF="mip2_vb_vb.html">
<IMG WIDTH="63" HEIGHT="24" ALT="previous" SRC="prev.png"></A>   
<BR>
<B> Next:</B> <A
 HREF="multiscenario_vb_vb.html">multiscenario_vb.vb</A>
<B> Up:</B> <A
 HREF="visual_basic_examples.html">Visual Basic Examples</A>
<B> Previous:</B> <A
 HREF="mip2_vb_vb.html">mip2_vb.vb</A></DIV>
<!--End of Navigation Panel-->

</BODY>
</HTML>
